plugins {
  id "groovy"
  id "idea"
  id "maven-publish"
  id "java-library"
  id "com.gradle.plugin-publish" version "0.10.1"
  id "com.github.hierynomus.license" version "0.15.0"
  id 'nebula.release' version '13.0.0'
}

// this magically becomes gradle.plugin.com.xebialabs
group = 'com.xebialabs'
defaultTasks 'build'

repositories { jcenter() }

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  withSourcesJar()
  withJavadocJar()
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

dependencies {
  implementation gradleApi()
  implementation "com.typesafe:config:1.2.1"

  testImplementation gradleTestKit()
  testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
  testImplementation 'com.netflix.nebula:nebula-test:7.8.5'
}

// This disables the pedantic doclint feature of JDK8
if (JavaVersion.current().isJava8Compatible()) {
  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
  }
}

pluginBundle {
  website = 'http://www.xebialabs.com/'
  vcsUrl = 'https://github.com/xebialabs/gradle-xl-dependency-plugin'
  description = 'XebiaLabs dependency plugin'
  tags = ['xebialabs', 'plugins', 'dependencies']

  plugins {
    dependencyPlugin {
      id = 'com.xebialabs.dependency'
      displayName = 'XebiaLabs dependency plugin'
    }
    dependencyBasePlugin {
      id = 'com.xebialabs.dependency.base'
      displayName = 'XebiaLabs dependency base plugin'
    }
  }

  mavenCoordinates {
    groupId = 'com.xebialabs'
    artifactId = 'gradle-xl-dependency-plugin'
  }
}

publishing {
  publications {
    mavenJava( MavenPublication ) {
      groupId "gradle.plugin.com.xebialabs"
      from components.java
    }
  }
}

tasks.release.dependsOn([build, publishPlugins])
